AC_INIT(Eris/Entity.cpp)

dnl Information

ERIS_MAJOR=0
ERIS_MINOR=9
ERIS_MICRO=7
ERIS_VERSION=$ERIS_MAJOR.$ERIS_MINOR.$ERIS_MICRO

AC_SUBST(ERIS_VERSION)

dnl versioning info for libtool
ERIS_CURRENT=1
ERIS_REVISION=0
ERIS_AGE=0
ERIS_VERSION_INFO=$ERIS_CURRENT:$ERIS_REVISION:$ERIS_AGE

ERIS_REQUIRES=""
WFMATH_LIB="-lwfmath"

AC_SUBST(ERIS_VERSION_INFO)

AM_INIT_AUTOMAKE(eris, $ERIS_VERSION)
AM_MAINTAINER_MODE

AM_CONFIG_HEADER(config.h)	

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CXX
AC_STDC_HEADERS
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

CXXFLAGS="$CXXFLAGS -Wall -ansi"

dnl Make sure we've got sensible versions of the sources (eg to get snprintf)

AC_DEFINE(_XOPEN_SOURCE, 500, [This is XOpen source])
AC_DEFINE(_ISOC99_SOURCE,1, [This is C99 source])

AC_LANG_SAVE
AC_LANG_CPLUSPLUS

PKG_CHECK_MODULES(SIGC, sigc++-1.2 >= 1.2,
    [
        CPPFLAGS="$CPPFLAGS $SIGC_CFLAGS"
        LDFLAGS="$LDFLAGS $SIGC_LIBS"
        ERIS_REQUIRES="$ERIS_REQUIRES sigc++-1.2"
    ],
    [
        AM_PATH_SIGC(1.0.3,[
	        CPPFLAGS="$CPPFLAGS $SIGC_CFLAGS"
	        LDFLAGS="$LDFLAGS $SIGC_LIBS"
		old_sigc_1_0='yes'
	        ],
		AC_MSG_ERROR(Couldn't find libsigc++ config [or config failed])
        )
    ])


dnl Check for libskstream library
PKG_CHECK_MODULES(SKSTREAM, skstream-0.2 ,
    [
        CPPFLAGS="$CPPFLAGS $SKSTREAM_CFLAGS"
        LDFLAGS="$LDFLAGS $SKSTREAM_LIBS"
        ERIS_REQUIRES="$ERIS_REQUIRES skstream-0.2"
    ],
    [
        AM_PATH_SKSTREAM(0.2.1, [
	        CPPFLAGS="$CPPFLAGS $SKSTREAM_CFLAGS"
	        LIBS="$LIBS $SKSTREAM_LIBS"],
	        AC_MSG_ERROR(Couldn't find skstream or version is insufficent)
        )
    ])



dnl Check for Atlas-C++ library
PKG_CHECK_MODULES(ATLAS, atlascpp-0.4 ,
    [
        CPPFLAGS="$CPPFLAGS $ATLAS_CFLAGS"
        LDFLAGS="$LDFLAGS $ATLAS_LIBS"
        ERIS_REQUIRES="$ERIS_REQUIRES atlascpp-0.4"
    ],
    [
        AM_PATH_ATLAS(0.4.4, [
	        CPPFLAGS="$CPPFLAGS $ATLAS_CFLAGS"
	        LIBS="$LIBS $ATLAS_LIBS"],
	        AC_MSG_ERROR(Couldn't find Atlas or version is insufficent)
        )
    ])

dnl Check for WFMath library
PKG_CHECK_MODULES(WFMATH, wfmath-0.2 ,
    [
        CPPFLAGS="$CPPFLAGS $WFMATH_CFLAGS"
        LDFLAGS="$LDFLAGS $WFMATH_LIBS"
        WFMATH_LIB="-lwfmath-0.2"
        ERIS_REQUIRES="$ERIS_REQUIRES wfmath-0.2"
    ],
    [
        AM_PATH_WFMATH(0.2.6,[
	        CPPFLAGS="$CPPFLAGS $WFMATH_CFLAGS"
	        LDFLAGS="$LDFLAGS $WFMATH_LIBS"
	        ],
	        AC_MSG_ERROR(Couldn't find wfmath config [or config failed])
        )
    ])


AC_LANG_RESTORE

dnl AC_MSG_NOTICE(Checking requirements for bindings)

AC_ARG_ENABLE(python, 
[  --enable-python         Enable building of Python bindings
],
	enable_python=no
)
AM_CONDITIONAL(PYTHON_BINDINGS, test "x$enable_python" = "xyes")

AC_ARG_ENABLE(perl, 
[  --enable-perl         Enable building of Python bindings
],
[
dnl testing for sigcperl should be sufficient, since that requires
dnl perl and sucks in its CFLAGS and LIBS
    if test "x$old_sigc_1_0" = "xyes"; then
	AM_PATH_SIGCPERL(0.1.3, [enable_perl='yes'],[
		AC_MSG_WARN(libsigcperl not found, not building perl bindings)
		enable_perl='no'
	])
    else
	PKG_CHECK_MODULES(SIGCPERL, sigcperl >= 0.2, [enable_perl='yes'],[
		AC_MSG_WARN(libsigcperl not found, not building perl bindings)
		enable_perl='no'
	])
    fi;
])
AM_CONDITIONAL(PERL_BINDINGS, test "x$enable_perl" = "xyes")

ERIS_POLL_PKGCONFIGS=
ERIS_POLL_GLIB_REQUIRES=

PKG_CHECK_MODULES(GLIB, glib-2.0 >= 2.0.0,
    [
	ERIS_POLL_PKGCONFIGS="$ERIS_POLL_PKGCONFIGS eris-poll-glib-1.0.pc"
	ERIS_POLL_GLIB_REQUIRES=glib-2.0
	have_glib='yes'
    ],
    [
        AM_PATH_GLIB(1.2.0,[
		ERIS_POLL_PKGCONFIGS="$ERIS_POLL_PKGCONFIGS eris-poll-glib.pc"
		have_glib='yes'
	        ],[
		AC_MSG_WARN(Couldn't find glib, not building glib poll)
		have_glib='no'
		]
        )
    ])
AM_CONDITIONAL(HAVE_GLIB, test "x$have_glib" = "xyes")

dnl AC_MSG_NOTICE(Checking for test environment)

AM_PATH_CPPUNIT(1.6.1,
    [
	enable_cppunit=yes
	AC_SUBST(CPPUNIT_CFLAGS)
	AC_SUBST(CPPUNIT_LIBS)
    ], [
	AC_MSG_WARN(Couldn't Find CppUnit - make check will not work)
	enable_cppunit=no
    ]
)

AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

SYSTEM_LIBS=""
ERIS_LIBS="-leris-1.0 $WFMATH_LIB -lm"
ERIS_CFLAGS=""
AC_SUBST(SYSTEM_LIBS)
AC_SUBST(ERIS_LIBS)
AC_SUBST(ERIS_CFLAGS)
AC_SUBST(ERIS_REQUIRES)
AC_SUBST(ERIS_POLL_PKGCONFIGS)
AC_SUBST(ERIS_POLL_GLIB_REQUIRES)

AC_OUTPUT([
Makefile
Eris/Makefile
bindings/Makefile
bindings/polls/Makefile
bindings/polls/glib/Makefile
test/Makefile
eris.dox
eris-config
eris.spec
eris-1.0.pc
eris-poll-glib-1.0.pc
])
